cmake_minimum_required(VERSION 3.14.0)
set(CMAKE_OSX_DEPLOYMENT_TARGET @SMTG_MACOS_DEPLOYMENT_TARGET@ CACHE STRING "")

set(vst3sdk_SOURCE_DIR "@SMTG_VST3_SDK_SOURCE_DIR@")
if(NOT vst3sdk_SOURCE_DIR)
    message(FATAL_ERROR "Path to VST3 SDK is empty!")
endif()

project(@SMTG_CMAKE_PROJECT_NAME@
    # This is your plug-in version number. Change it here only.
    # Version number symbols usable in C++ can be found in
    # source/version.h and ${PROJECT_BINARY_DIR}/projectversion.h.
    VERSION 1.0.0.0 
    DESCRIPTION "@SMTG_CMAKE_PROJECT_NAME@ VST 3 Plug-in"
)

set(SMTG_VSTGUI_ROOT "${vst3sdk_SOURCE_DIR}")

option(SMTG_ENABLE_AAX_PLUGIN "Enable AAX Plugin" @SMTG_ENABLE_AAX_PLUGIN_CLI@)
option(SMTG_ENABLE_AUV3_PLUGIN "Enable AUV3 Plugin" @SMTG_ENABLE_AUV3_PLUGIN_CLI@)
option(SMTG_ENABLE_STANDALONE "Enable Standalone" @SMTG_ENABLE_STANDALONE_CLI@)

smtg_enable_vst3_sdk()

smtg_add_vst3plugin(@SMTG_CMAKE_PROJECT_NAME@-vst3
    PACKAGE_NAME @SMTG_CMAKE_PROJECT_NAME@
    SOURCES_LIST
    source/version.h
    source/@SMTG_PREFIX_FOR_FILENAMES@cids.h
    source/@SMTG_PREFIX_FOR_FILENAMES@processor.h
    source/@SMTG_PREFIX_FOR_FILENAMES@processor.cpp
    source/@SMTG_PREFIX_FOR_FILENAMES@controller.h
    source/@SMTG_PREFIX_FOR_FILENAMES@controller.cpp
    source/@SMTG_PREFIX_FOR_FILENAMES@entry.cpp
)

set_target_properties(@SMTG_CMAKE_PROJECT_NAME@-vst3 PROPERTIES OUTPUT_NAME "@SMTG_CMAKE_PROJECT_NAME@")

#- VSTGUI Wanted ----
if(SMTG_ENABLE_VSTGUI_SUPPORT)
    target_sources(@SMTG_CMAKE_PROJECT_NAME@-vst3
        PRIVATE
            resource/@SMTG_PREFIX_FOR_FILENAMES@editor.uidesc
    )
    target_link_libraries(@SMTG_CMAKE_PROJECT_NAME@-vst3
        PRIVATE
            vstgui_support
    )
    smtg_target_add_plugin_resources(@SMTG_CMAKE_PROJECT_NAME@-vst3
        RESOURCES
            "resource/@SMTG_PREFIX_FOR_FILENAMES@editor.uidesc"
    )
endif(SMTG_ENABLE_VSTGUI_SUPPORT)
# -------------------

smtg_target_add_plugin_snapshots (@SMTG_CMAKE_PROJECT_NAME@-vst3
    RESOURCES
        resource/@SMTG_Processor_PLAIN_UUID@_snapshot.png
        resource/@SMTG_Processor_PLAIN_UUID@_snapshot_2.0x.png
)

target_link_libraries(@SMTG_CMAKE_PROJECT_NAME@-vst3
    PRIVATE
        sdk
)

smtg_target_configure_version_file(@SMTG_CMAKE_PROJECT_NAME@-vst3)

if(SMTG_MAC)
    smtg_target_set_bundle(@SMTG_CMAKE_PROJECT_NAME@-vst3
        BUNDLE_IDENTIFIER @SMTG_PLUGIN_IDENTIFIER@
        COMPANY_NAME "@SMTG_VENDOR_NAME@"
    )
    smtg_target_set_debug_executable(@SMTG_CMAKE_PROJECT_NAME@-vst3
        "/Applications/VST3PluginTestHost.app"
        "--pluginfolder;$(BUILT_PRODUCTS_DIR)"
    )
elseif(SMTG_WIN)
    target_sources(@SMTG_CMAKE_PROJECT_NAME@-vst3 PRIVATE 
        resource/win32resource.rc
    )
    if(MSVC)
        set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT @SMTG_CMAKE_PROJECT_NAME@-vst3)

        smtg_target_set_debug_executable(@SMTG_CMAKE_PROJECT_NAME@-vst3
            "$(ProgramW6432)/Steinberg/VST3PluginTestHost/VST3PluginTestHost.exe"
            "--pluginfolder \"$(OutDir)/\""
        )
    endif()
endif(SMTG_MAC)

if(SMTG_ENABLE_AAX_PLUGIN)
    add_subdirectory(source/aax)
endif(SMTG_ENABLE_AAX_PLUGIN)

if(SMTG_ENABLE_AUV3_PLUGIN)
    add_subdirectory(source/auv3)
endif(SMTG_ENABLE_AUV3_PLUGIN)

if(SMTG_ENABLE_STANDALONE)
    add_subdirectory(source/standalone)
endif(SMTG_ENABLE_STANDALONE)

if(WIN32)
    find_program(INNO NAMES "C:/Program Files (x86)/Inno Setup 6/Compil32.exe"
        HINTS "C:/Program Files (x86)/Inno Setup 6" "C:/Program Files/Inno Setup 6")

    if(INNO)
        install(DIRECTORY "${CMAKE_BINARY_DIR}/VST3/Debug/@SMTG_CMAKE_PROJECT_NAME@.vst3" DESTINATION . COMPONENT @SMTG_CMAKE_PROJECT_NAME@VST3) # Dot sets directly in target folder.
        install(DIRECTORY "${CMAKE_BINARY_DIR}/AAXPLUGIN/Debug/@SMTG_CMAKE_PROJECT_NAME@.aaxplugin" DESTINATION . COMPONENT @SMTG_CMAKE_PROJECT_NAME@AAXPLUGIN) # Dot sets directly in target folder.
        install(DIRECTORY "${CMAKE_BINARY_DIR}/APP/Debug/@SMTG_CMAKE_PROJECT_NAME@-app" DESTINATION . COMPONENT @SMTG_CMAKE_PROJECT_NAME@APP) # Dot sets directly in target folder.

        # Start Menu Shortcuts: executable without .exe and text label
        set(CPACK_PACKAGE_EXECUTABLES "@SMTG_CMAKE_PROJECT_NAME@" "@SMTG_CMAKE_PROJECT_NAME@")
        set(CPACK_CREATE_DESKTOP_LINKS "@SMTG_CMAKE_PROJECT_NAME@") # Desktop link

        # Inno Setup is supported from CMake 3.27.
        set(CPACK_GENERATOR INNOSETUP)

        # This must always be after all CPACK\_\* variables are defined
        set(CPACK_INNOSETUP_EXTRA_SCRIPTS ${CMAKE_CURRENT_LIST_DIR}/resource/installer.iss)
        set(CPACK_INNOSETUP_USE_MODERN_WIZARD ON)
        include(CPack)
    else()
        message(WARNING "Inno Setup not found. Download it from http://www.jrsoftware.org/download.php/is.exe if you want to create installer of your plugin")
    endif(INNO)
elseif(CMAKE_HOST_APPLE)
# TODO
elseif(LINUX)
    # TODO
endif(WIN32)